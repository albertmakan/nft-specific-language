Script:
    package_section=PackageSection
    contract_section=ContractSection
    administration=AdministrationSection
;

PackageSection:
    'packages' '{'
        packages*=PackageImport
    '}'
;

PackageImport:
    'using' id=FQN ('as' alias=ID)?
;

ContractSection:
    contracts+=ContractDefinition
;

ContractDefinition:
    'contract' name=ID 'combines' '{'
        implementation=ContractImplementation
    '}'
;

ContractImplementation:
    methods*=Method
;

Method:
    name=FQN (params=Parameters)?
;

AdministrationSection:
    'administration' '{'
        main_administrator=MainAdministrator
        extension_administrators*=ExtensionAdministrator
    '}'
;

MainAdministrator:
    'managed' 'by' Administrator
;

ExtensionAdministrator:
    'extended' 'by' Administrator
;

Administrator:
    method=Method 'having' 'partial' 'access' '{'
        contract_administrators+=ContractAdministrator
    '}'
;

ContractAdministrator:
    'to' contract=[ContractDefinition] '{'
        methods*=Method
    '}'
;

Parameters:
    'with' parameters*=Parameter['and']
;

Parameter:
    literal=Literal name=ID
;

Literal:
    Percentage | INT | FLOAT | STRING | AddressSet | Address
;

Percentage: percent=/(100|[1-9]?\d)/ '%';

Address: value=/0x[a-fA-F0-9]{40}/;

AddressSet: '[' values*=Address[','] ']';

FQN: ID+['.'];

Comment: /\/\/.*$/;
