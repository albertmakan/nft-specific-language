Script:
    (_local_packages=LocalPackageSection)?
    (package=PackageSection)?
    contract=ContractSection
    administration=AdministrationSection
;
PackageSection:
    'packages' '{' packages*=PackageImport '}'
;
PackageImport:
    'using' id=FQN ('as' alias=ID)?
;
FQN: ID+['.'];

ContractSection:
    contracts+=ContractDefinition
;
ContractDefinition:
    'contract' name=ID 'combines' '{' implementation=ContractImplementation '}'
;
ContractImplementation:
    methods*=Method
;
Method:
    name=FQN (params=Parameters)?
;
AdministrationSection:
    'administration' '{' main_administrator=MainAdministrator extension_administrators*=ExtensionAdministrator '}'
;
MainAdministrator:
    'managed' 'by' Administrator
;
ExtensionAdministrator:
    'extended' 'by' Administrator
;
Administrator:
    package=FQN (params=Parameters)? 'having' 'partial' 'access' '{' contract_administrators+=ContractAdministrator '}'
;
ContractAdministrator:
    'to' contract=[ContractDefinition] '{' methods*=Method '}'
;
Parameters:
    'with' parameters*=Parameter['and']
;
Parameter:
    value=Literal name=ID
;
Literal:
    /(100|[1-9]?\d)%/ | INT | FLOAT | STRING
;
LocalPackageSection:
    '_local_packages' '{' local_packages*=LocalPackage '}'
;
LocalPackage:
    'package' name=FQN
;
Comment: /\/\/.*$/;